//=========================================================================================================
//
// Doc # : TBD
// Date  : 14 Aug 2018            
// Last Updated:  May 8 2019 (BB)
// MTJ Model Version: 0.6 ENG 2
// Associated PDK version: 1.3-3.0 
// MTJ state convention:  High (MAX) resistance state (AP) : 0  |   Low (MIN) resistance state (P) : 1 
// ---------------------------------------------------------------------------------------------------


// Behtash  Behin-Aein
// MRAM Device Group
// Behtash.Behin-Aein@GLOBALFOUNDRIES.com
// Magnetic Tunnel Junction compact model
//======================================================================================================	
	
ahdl_include "mtj_mods.va"
 subckt mtj_fls (r s) 
  parameters   initial_state= 1    
  ic mz = 2*initial_state-1 
  //_____________________________________________________________________________________________________________________________________________
  // initial_state:   Binary. When set to 0, MTJ starts in the high resistance (AP) state. Otherwhise MTJ starts in the low resistance (P) state.
  //                  This state remains fixed for DC simulations but can change in the AC/Transient simulation depending on MTJ voltage/current.   
  //_____________________________________________________________________________________________________________________________________________
  
  
  //***************************
  //    node:   s                                                 
  //---------------------------                                
  //       Free Layer
  //---------------------------                                
  //           MgO
  //---------------------------                                
  //     Reference Layer          
  //---------------------------                                   
  //            pb
  //--------------------------- Parasitic Resistance
  //    node:   r 
  //***************************
  
  
  //--- Parasitic Res-------------------------        
  r_pb (r pb)  resistor r = 80
  //------------------------------------------
   
   
   parameters rnd1=0 rnd2=0 rnd3=0 rnd4=0 rnd5=0 rnd6=0 rnd7=0 rnd8=0
   statistics {  
    mismatch {
      vary rnd1 dist=gauss std=1 percent=no
      vary rnd2 dist=gauss std=1 percent=no
      vary rnd3 dist=gauss std=1 percent=no
      vary rnd4 dist=gauss std=1 percent=no
      vary rnd5 dist=gauss std=1 percent=no
      vary rnd6 dist=gauss std=1 percent=no
      vary rnd7 dist=gauss std=1 percent=no
      vary rnd8 dist=gauss std=1 percent=no
    }
  }   
   
   
  xr    (pb pt mz) rmtj   mc=mc_sw initial_state=initial_state Temp_C=temp  rnd1=rnd1 rnd2=rnd2 rnd3=rnd3 rnd4=rnd4 rnd5=rnd5 rnd6=rnd6 rnd7=rnd7 rnd8=rnd8
  vsrc  (pt s)     vsource   dc=0    
  ccvs  (0 n)      bsource   v=i("vsrc:0")    
  xSW   (0 n mz 0) Dyn  initial_state=initial_state  Temp_C=temp   
							      
ends mtj_fls

